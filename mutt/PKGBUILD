# Maintainer: notfoss <AT> gmx.com
# Contributors: tobias [tobias [at] archlinux.org], Gaetan Bisson <bisson@archlinux.org>

# Features:

# * Sidebar patch: http://www.lunar-linux.org/mutt-sidebar/
# * Use berkeley db instead of gdbm.
# * Email sending and retrieving functionality has been stripped. I use msmtp and offlineimap for that.
# * Remove other unnecessary features, see ./configure below.

pkgname="mutt"
pkgver="1.5.23"
pkgrel="1"
pkgdesc="Small but very powerful text-based mail client"
url="http://www.mutt.org/"
license=("GPL")
backup=("etc/Muttrc")
arch=("i686" "x86_64")
optdepends=("smtp-forwarder: to send mail")
depends=("gpgme" "ncurses" "db" "mime-types")
source=("https://bitbucket.org/${pkgname}/{$pkgname}/downloads/${pkgname}-${pkgver}.tar.gz"
        "patch-1.5.23.sidebar.20140412.txt")
sha1sums=("8ac821d8b1e25504a31bf5fda9c08d93a4acc862"
          "8b86f148d8e1a0b37b0b77268349f4c94e797561")

groups=("custom_packages")

install="install"

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # sidebar patch
    patch -p1 -i "${srcdir}"/patch-1.5.23.sidebar.20140412.txt

    # fix automake issue
    autoreconf -vfi
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --enable-gpgme \
        --disable-pop \
        --disable-imap \
        --disable-smtp \
        --enable-hcache \
        --with-curses=/usr \
        --with-regex \
        --without-gss \
        --without-ssl \
        --without-sasl \
        --without-idn \
        --without-gdbm \
        --without-qdbm \
        --with-bdb=/usr \

    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    # remove unnecessary or conflicting files
    rm "${pkgdir}"/etc/mime.types{,.dist}
    rm "${pkgdir}"/usr/bin/{flea,muttbug}
    rm "${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1

    # install Muttrc.gpg file
    install -Dm644 contrib/gpg.rc "${pkgdir}"/etc/Muttrc.gpg.dist
}
