# Borrowed from the Arch Linux ABS.
# Contributors: tobias [tobias [at] archlinux.org], Gaetan Bisson <bisson@archlinux.org>
# Maintainer: static DOT vortex AT gmx.com

# Features:

# * Ignore Thread patch: http://ben.at.tanjero.com/patches/ignore-thread-1.5.20.patch
# * Uses berkeley db instead of gdbm.
# * Email sending and retrieving functionality has been stripped. I use msmtp and offlineimap for that.
# * Stripped other unnecessary features, see ./configure below.

pkgname="mutt"
pkgver="1.5.22"
pkgrel="1"
pkgdesc="Small but very powerful text-based mail client"
url="http://www.mutt.org/"
license=("GPL")
backup=("etc/Muttrc")
arch=("i686" "x86_64")
optdepends=("smtp-forwarder: to send mail")
depends=("gpgme" "ncurses" "db" "mime-types")
source=("ftp://ftp.mutt.org/mutt/devel/${pkgname}-${pkgver}.tar.gz"
        "ignore-thread-1.5.20.patch")
sha1sums=("728a114cb3a44df373dbf1292fc34dd8321057dc"
          "c5597bb6bd4d0995e396bb45a2489702f2ce43d2")

groups=("custom_packages")

install="install"

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    patch -p1 -i "${srcdir}"/ignore-thread-1.5.20.patch

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --enable-gpgme \
        --disable-pop \
        --disable-imap \
        --disable-smtp \
        --enable-hcache \
        --with-curses=/usr \
        --with-regex \
        --without-gss \
        --without-ssl \
        --without-sasl \
        --without-idn \
        --without-gdbm \
        --without-qdbm \
        --with-bdb=/usr \

    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    rm "${pkgdir}"/etc/mime.types{,.dist}
    rm "${pkgdir}"/usr/bin/{flea,muttbug}
    rm "${pkgdir}"/usr/share/man/man1/{flea,muttbug}.1
    install -Dm644 contrib/gpg.rc "${pkgdir}"/etc/Muttrc.gpg.dist
}
